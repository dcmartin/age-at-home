#!/bin/csh -fb

set dir = "$cwd/$0:h"

if ($?share == 0) set share = "/usr/share/hassio"
if (! -d "$share") then
  echo "ERROR: cannot locate directory $share"
  exit
endif

if ($?config == 0) set config = "$share/homeassistant"
if ($?addons == 0) set addons = "$share/addons/local"

# copy CONFIGURATION
set yamls = ( scripts known_devices configuration groups history automations templates logger customize secrets zones )
echo -n "Copying YAML ["
foreach yaml ( $yamls )
  if (-e "$dir/config/$yaml.yaml") then
    echo -n "$yaml "
    cp "$dir/config/$yaml.yaml" "$config/$yaml.yaml"
  else
    echo "NO SUCH FILE $dir/config/$yaml.yaml"
    exit 1
  endif
end
echo "]"

# copy COMPONENTS
if (-d "$dir/config/custom_components") then
  echo "Copying COMPONENTS"
  cp -r "$dir/config/custom_components/" "$config/custom_components"
else
  echo "WARN: cannot locate $dir/config/custom_components"
endif

# copy ADDONS
echo "Copying ADDONS"
foreach i ( "$dir"/addons/* )
  echo -n "$i:t ["
  if (-d "$i") then
    mkdir -p "$addons/$i:t"
    foreach j ( "$i"/* )
      echo -n "$j:t "
      if (-d "$j") then
        mkdir -p "$addons/$i:t/$j:t"
        foreach k ( "$j"/* )
          echo -n "$k:t "
          cp "$k" "$addons"/"$i:t"/"$j:t"/"$k:t"
        end
      else
        cp "$j" "$addons"/"$i:t"/"$j:t"
      endif
    end
  else
    cp "$j" "$addons"/"$i:t"/"$j:t"
  endif
  echo "]"
end
