#!/bin/tcsh

set dir = "$cwd"

if ($?share == 0) set share = "/usr/share/hassio"
if (! -d "$share") then
  echo "ERROR: cannot locate directory $share"
  exit
endif

if ($?config == 0) set config = "$share/homeassistant"
if ($?addons == 0) set addons = "$share/addons/local"

if (! -d "$config") then
  echo "[Info] making $config"
  mkdir -p "$config"
endif

if (! -d "$addons") then
  echo "[Info] making $addons"
  mkdir -p "$addons"
endif

# copy CONFIGURATION
# set yamls = ( ui-lovelace script known_devices configuration group history automation logger customize secrets zone scene input_boolean input_select input_text )
set yamls = ( `find config -name "*.yaml" -printf "%f\n" | sed "s/\.yaml//"` )

echo -n "Copying YAML ["
foreach yaml ( $yamls )
  if (-e "$dir/config/$yaml.yaml") then
    if (-e "$dir/$yaml.yaml") then
      echo -n "$yaml ($dir/$yaml) "
      cat "$dir/config/$yaml.yaml" "$dir/$yaml.yaml" >! "$config/$yaml.yaml"
    else
      echo -n "$yaml "
      cp "$dir/config/$yaml.yaml" "$config/$yaml.yaml"
    endif
  else
    echo "NO SUCH FILE $dir/config/$yaml.yaml"
    exit 1
  endif
end
echo "]"

# copy COMPONENTS
if (-d "$dir/config/custom_components") then
  echo "Copying COMPONENTS"
  cp -r "$dir/config/custom_components/" "$config/custom_components"
else
  echo "WARN: cannot locate $dir/config/custom_components"
endif

# copy ADDONS
if (-d "$dir/addons") then
  echo "Copying ADDONS"
  foreach ao ( "$dir"/addons/* )
    if (! -d "$ao") then
      echo "skipping $ao"
      continue
    endif
    echo -n "$ao:t ["
    if (-d "$ao") then
      if ( -e "$dir/$ao:t.json" ) set json = "$dir/$ao:t.json"
      mkdir -p "$addons/$ao:t"
      foreach aoe ( "$ao"/* )
	# if <addon.json> found ($?json) get ".options" only and replace in primary
        if ( "$aoe:t" == "config.json" && $?json ) then
          echo -n "$json "
	  set options = `jq -r '.' "$json"`
	  jq '.|.options='"$options" "$aoe" >! "$addons"/"$ao:t"/"$aoe:t"
        else if (-d "$aoe") then
          echo -n "$aoe:t "
          mkdir -p "$addons/$ao:t/$aoe:t"
          foreach k ( "$aoe"/* )
            echo -n "$k:t "
            cp "$k" "$addons"/"$ao:t"/"$aoe:t"/"$k:t"
          end
        else
          echo -n "$aoe:t "
          cp "$aoe" "$addons"/"$ao:t"/"$aoe:t"
        endif
      end
    else
      cp "$aoe" "$addons"/"$ao:t"/"$aoe:t"
    endif
    echo "]"
    unset json
  end
else if (-d "$addons") then
  echo "[Info] Removing $addons"
  rm -rf "$addons"
endif

